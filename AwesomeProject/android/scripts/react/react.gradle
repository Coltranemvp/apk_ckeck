task reactBuildAssets(type: Exec) {
    group "react"

    workingDir "${rootProject.projectDir}/scripts/react"
    commandLine './react-build.sh'

    doLast {
        println "React assets were successfully built!"
    }
}

tasks.preBuild.dependsOn(reactBuildAssets)

task reactCleanAssets(type: Exec) {
    group "react"

    workingDir "${rootProject.projectDir}/scripts/react"
    commandLine './react-clean.sh'

    doLast {
        println "React assets were successfully cleaned!"
    }
}

tasks.clean.dependsOn(reactCleanAssets)

task reactRebuildProject {
    group "react"

    dependsOn 'clean'
    dependsOn 'assemble'

    assemble.shouldRunAfter(clean)
}

afterEvaluate {
    task reactPublishArtifactsToMavenLocal {
        group "react"

        dependsOn 'reactRebuildProject'
        dependsOn 'publishAllPublicationsToMavenLocalRepository'
        publishAllPublicationsToMavenLocalRepository.shouldRunAfter(reactRebuildProject)

        doLast {
            println "React artifacts were successfully published to mavenLocal()!"
        }
    }

    task reactPublishArtifactsToSonatype {
        group "react"

        dependsOn 'reactRebuildProject'
        dependsOn 'publishAllPublicationsToSonatypeRepository'
        publishAllPublicationsToSonatypeRepository.shouldRunAfter(reactRebuildProject)

        doLast {
            println "React artifacts were successfully published to sonatype!"
        }
    }
}